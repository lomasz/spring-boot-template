plugins {
    id "java"
    id "org.springframework.boot" version "3.2.0"
    id "io.spring.dependency-management" version "1.1.4"
    id "jacoco"
    id "org.owasp.dependencycheck" version "8.4.3"
    id "com.diffplug.spotless" version "6.23.0"
    id "com.coditory.integration-test" version "1.4.5"
}

ext {
    jacocoVersion = "0.8.11"
    junitVersion = "5.10.1"
    liquibaseVersion = "4.25.0"
    lombokVersion = "1.18.30"
    mapStructVersion = "1.5.5.Final"
    mockitoVersion = "5.7.0"
    swaggerVersion = "2.2.0"
    h2Version = "2.2.224"
}

group = "com.github.lomasz.spring.boot.template"
version = "0.0.1-SNAPSHOT"

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven { url "https://oss.jfrog.org/artifactory/oss-snapshot-local/" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$swaggerVersion"

    implementation "org.liquibase:liquibase-core:$liquibaseVersion"

    implementation "ch.qos.logback:logback-classic:1.4.12" // Because of: CVE-2023-6378
    implementation "ch.qos.logback:logback-core:1.4.12" // Because of: CVE-2023-6378

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    implementation "org.mapstruct:mapstruct:$mapStructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapStructVersion"

    runtimeOnly "com.h2database:h2:$h2Version"

    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"

    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
}

processResources {
    expand(project.properties)
}

clean {
    delete "out"
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
//        palantirJavaFormat()
        indentWithSpaces(4)
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
    executionData(fileTree(layout.buildDirectory.get().asFile).include("jacoco/*.exec"))
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ["**/*Application*",
                              "**/config/**/*",
                              "**/exception/**/*",
                              "**/model/**/*",
                              "**/mapper/**/*",
                              "**/repository/**/*"])
        }))
    }
}

dependencyCheck {
    failBuildOnCVSS = 7
    format = "HTML"
}

tasks.testAll {
    finalizedBy tasks.jacocoTestReport
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    finalizedBy tasks.jacocoTestReport

    testLogging {
        events "passed", "skipped", "failed"
    }
}
